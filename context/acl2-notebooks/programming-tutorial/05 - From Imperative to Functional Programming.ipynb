{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# From Imperative to Functional Programming\n",
    "\n",
    "You're probably familiar with programming in imperative languages, like Java or C++, and you may be feeling uncomfortable with programming in a functional language like ACL2. Here are some notes to help you make this transition.\n",
    "\n",
    "## Watch Your Parentheses\n",
    "\n",
    "As you've seen, ACL2 uses parentheses extensively. In fact, parentheses are the most basic part of the ACL2 syntax, so it's important to use them correctly. If ACL2 is giving you a weird error message, or if it simply fails to respond, chances are your parentheses are not balanced, e.g., there is a missing or an extra parentheses somewhere. Use your editor to help you find these types of parentheses errors. It helps if you have a programmer's editor that can reindent expressions, but it's also helpful if the editor can simply match opening and closing parentheses.\n",
    "\n",
    "One important thing that sometimes trips people up is that parentheses denote function invocations in ACL2. So if you see an expression like `(a b c)`, that is a function call! The function `a` is being called with values `b` and `c`. \n",
    "\n",
    "> **Warning!** The expression `(3)` is a syntax error in ACL2! It is an attempt to call the \"function\" 3, and of course there is no such function.\n",
    "\n",
    "> **Warning!** This is another ACL2 syntax `((+ 1 2))`. The opening parenthesis starts a function call, so the first thing after the parenthesis must be a function name. But here, the first thing after the openoing parenthesis is another opening parenthesis, not a function.\n",
    "\n",
    "> **Warning!** You are probably used to thinking of `x` and `(x)` as being the same thing, but they are not at all the same thing in ACL2.\n",
    "\n",
    "## Variable Names\n",
    "\n",
    "Languages like Java and C++ allow only letters, numbers, and a handful of special characters in variable names. But ACL2 allows almost every character in variable names, with just a few exceptions, e.g., parentheses. In particular, `x-y` is not the expression \"x minus y\"! Instead, it is simply the variable with name \"x dash y\". It is convention in ACL2 to use dashes instead of underscores to separate words in variables or function names, so we would define the function `sum-squares` instead of `sum_squares`. Also, names in ACL2 are caseinsensitive, so we would never use CamelCase. The function `sumSquares` will be displayed by ACL2 as `SUMSQUARES` which is pretty much unreadable. So use a single dash, \"-\", to separate words in names.\n",
    "\n",
    "## Sequence\n",
    "\n",
    "Sequence is the basic flow control in imperative languages. You write one sentence, then another, and the computer will execute the first sentence, then the second. It's such a simple thing that you probably don't even think of this as control-flow!\n",
    "\n",
    "In ACL2, each function is made up of a single expression. So how can you \"do one thing, then another\"? The answer is the this is done in ACL2 by having a function call inside another function call. This is called function composition, and it's best shown with an example. Consider this sequence of code in a Java-like program:\n",
    "\n",
    "    x = a / 2\n",
    "    y = b + 1\n",
    "    return x - y / x\n",
    "\n",
    "In ACL2, you would turn these three statements into a single expression, which in this case means we repeat the value of `x`:\n",
    "\n",
    "    (- (/ a 2)\n",
    "       (/ (+ b 1)\n",
    "          (/ a 2)))\n",
    "\n",
    "Remember: When you have to take multiple steps to find the value of an expression, your first instinct may be to write multiple lines, i.e., one line per step. But in ACL2, you have to write a single expression, which may consist of many sub-expressions.\n",
    "\n",
    "## Selection\n",
    "\n",
    "Selection is the if-then-else control-flow in languages like Java. ACL2 has an `(if a b c)` function which has the same purpose, but you should remember that `(if a b c)` is an expression, as opposed to a statement. Remember, there are no \"statements\" in ACL2, so everything is an expression.\n",
    "\n",
    "Languages like Java or C++ have a special ternary operator that is actually exactly like the if-expressions in ACL2. In those languages, you can write code like\n",
    "\n",
    "    f(x>0 ? x : -x)\n",
    "\n",
    "And in ACL2, you would say\n",
    "\n",
    "    (f (if (> x 0)\n",
    "           x\n",
    "           (- x)))\n",
    "\n",
    "I usually prefer to have the `(if ...)` expressions at the top of the expressions, so I would normally write this instead, which I think is more readable:\n",
    "\n",
    "    (if (> x 0)\n",
    "        (f x)\n",
    "        (f (- x)))\n",
    "\n",
    "## Iteration\n",
    "\n",
    "Iteration, or loops, is the other control-flow structure in languages like Java or C++. There is no equivalent structure in ACL2, so instead of loops you have to write down recursive functions. For example, to compute $1+2+\\cdots+n$ in Java, you may write something like this:\n",
    "\n",
    "    sum = 0\n",
    "    for (i=1; i<=n; i++)\n",
    "        sum = sum + n\n",
    "\n",
    "In ACL2, you would instead define the recursive function `(sum n)`, and the first step is to think of the corresponding defining equations:\n",
    "\n",
    "    (sum n) = 0,                             if n=0 or anything other than a positive integer\n",
    "    (sum n) = (+ (sum (- n 1)) n),    if n is a positive integer\n",
    "\n",
    "Then the \"loop\" can be written as follows:\n",
    "\n",
    "    (define sum (n :nat) :nat\n",
    "      (if (zp n)\n",
    "          0\n",
    "          (+ (sum (- n 1)) n)))\n",
    "\n",
    "## Nested Iteration\n",
    "\n",
    "Now, suppose you want to find $(1) + (1\\times2) + (1\\times2\\times3) + \\cdots + (1\\times2\\times\\cdots\\times n)$. In languages like Java, you may find this sum using a nested loop:\n",
    "\n",
    "    sum = 0\n",
    "    for (i=1; i<=n; i++)\n",
    "        prod = 1\n",
    "        for (j=1; j<=i; j++)\n",
    "            prod *= j\n",
    "        sum += prod\n",
    "\n",
    "In ACL2, each loop is its own recursive function, so you have to write two function definitions to compute this expression. First, here is the inner loop, or the `prod` function:\n",
    "\n",
    "    (definec prod (i :nat) :nat\n",
    "      (if (zp i)\n",
    "          1\n",
    "          (* (prod (- i 1)) \n",
    "             i)))\n",
    "\n",
    "Now we can define the outer loop as follows:\n",
    "\n",
    "    (definec sum-of-prods (n :nat) :nat\n",
    "      (if (zp n)\n",
    "          0\n",
    "          (+ (sum-of-prods (- n 1))\n",
    "             (prod n))))\n",
    "\n",
    "## Assignment Statements\n",
    "\n",
    "You are probably used to writing code that manipulates the values of variables by using assignment statements, but there is nothing like it in functional programming. Generally speaking, if you think you need a variable in order to compute what you want, you're probably not thinking about the problem in the right way. Try again to find a good set of defining equations for your problem.\n",
    "\n",
    "But there is a trick you can use to define a new variable. Function arguments are like \"variables\", but they can never change values. But if you simply seen a new variable, you can sometimes do this by building a new function.\n",
    "For example, suppose you want to write a function that calculates $\\underbrace{n + n + \\cdots + n}_{n\\text{ times}}$. In Java, you could write something like this:\n",
    "\n",
    "    sum = 0\n",
    "    for (i=0; i<n; i++)\n",
    "        sum += n\n",
    "\n",
    "You may think that this is similar to this function in ACL2:\n",
    "\n",
    "    (definec f (n :nat) :nat\n",
    "      (if (zp n)\n",
    "          0\n",
    "          (+ (f (- n 1)) n)))\n",
    "\n",
    "But these functions do not compute the same value! The problem is that when the function computes `(f (- n 1))`, it has a smaller value of `n`, so it does not keep adding the same value each time through the loop. You should recognize that what this function is computing is $1+2+\\cdots+n$.\n",
    "\n",
    "In the Java code, this works fine because the variable `i` is used to control the number of executions in the loop, while the loop adds `n` each time through the loop. In ACL2, we need to do a similar trick, so that we have a one variable to go through the loop and a different variable to hold the number we need to add each time. We can do this by writing a more general function that has these two variables:\n",
    "\n",
    "    (define -f (i :nat n :nat) :nat\n",
    "      (if (zp i)\n",
    "          0\n",
    "          (+ (-f (- i 1) n)\n",
    "             n)))\n",
    "\n",
    "The function we want is now a special case, where we use `n` to initialize `i` and `n`:\n",
    "\n",
    "    (define f (n :nat) :nat\n",
    "      (-f n n))\n",
    "\n",
    "See how `n` is used for both `i` and `n` in the call to `-f`? That ensure we execute the \"loop\" `n` times, and add `n` each time through the loop.\n",
    "\n",
    "Incidentally, users should only call `f`; they should never call `-f` direction. I use the leading `-` in `-f` to indicate that this is a \"hidden\" function, the was a leading underscore, as in `_f`, is used in some languages (like Python) to indicate private functions.\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "ACL2",
   "language": "acl2",
   "name": "acl2"
  },
  "language_info": {
   "codemirror_mode": "Common Lisp",
   "file_extension": ".lisp",
   "mimetype": "text/x-common-lisp",
   "name": "acl2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
